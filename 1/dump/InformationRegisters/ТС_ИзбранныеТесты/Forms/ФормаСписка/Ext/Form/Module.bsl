///////////////////////////////////////////////////////////////
// СОБЫТИЯ ФОРМЫ

//*************************************************************
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//Отбор по пользователю
	ИнициализироватьОтборПоПользователю();
	
	//Отбор по курсу
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Владелец") Тогда
		ЭтаФорма.Курс = Параметры.Отбор.Владелец;
		ЭтаФорма.Элементы.Курс.Доступность = Ложь;
	Иначе
		ИнициализироватьОтборПоКурсу();
	КонецЕсли;	
	
КонецПроцедуры

//*************************************************************
&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ИнициализироватьОтборПоКурсу();	
	
КонецПроцедуры



///////////////////////////////////////////////////////////////
// ДЕЙСТВИЯ ФОРМЫ

//*************************************************************
&НаКлиенте
Процедура КурсПриИзменении(Элемент)
	
	ИнициализироватьОтборПоКурсу();
	
КонецПроцедуры

//*************************************************************
&НаСервере
Процедура ИнициализироватьОтборПоКурсу()
	
	ЭлементыОтбора = ЭтаФорма.Список.КомпоновщикНастроек.Настройки.Отбор.Элементы;
	ОтборДобавлен = Ложь;
	ПолеКДВладелец = Новый ПолеКомпоновкиДанных("Курс");

	//Изменим существующий отбор
	Для Каждого Элемент Из ЭлементыОтбора Цикл
		Если Элемент.ЛевоеЗначение = ПолеКДВладелец Тогда
			Элемент.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			Элемент.Использование = Истина;
			Элемент.ПравоеЗначение = ЭтаФорма.Курс;
			Элемент.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
			ОтборДобавлен = Истина;
		КонецЕсли
	КонецЦикла;
	
	//Добавим новый отбор
	Если НЕ ОтборДобавлен Тогда
		
		ОтборПоКурсу = ЭлементыОтбора.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборПоКурсу.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборПоКурсу.Использование = Истина;
		ОтборПоКурсу.ЛевоеЗначение = ПолеКДВладелец;		
		ОтборПоКурсу.ПравоеЗначение = ЭтаФорма.Курс;
		ОтборПоКурсу.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		
	КонецЕсли;		
	
КонецПроцедуры

//*************************************************************
&НаСервере
Процедура ИнициализироватьОтборПоПользователю()
	
	ЭлементыОтбора = ЭтаФорма.Список.КомпоновщикНастроек.Настройки.Отбор.Элементы;
	ОтборДобавлен = Ложь;
	ПолеКД = Новый ПолеКомпоновкиДанных("Пользователь");

	//Изменим существующий отбор
	Для Каждого Элемент Из ЭлементыОтбора Цикл
		Если Элемент.ЛевоеЗначение = ПолеКД Тогда
			Элемент.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			Элемент.Использование = Истина;
			Элемент.ПравоеЗначение = ПараметрыСеанса.ТС_ТекущийПользователь;
			ОтборДобавлен = Истина;
		КонецЕсли
	КонецЦикла;
	
	//Добавим новый отбор
	Если НЕ ОтборДобавлен Тогда
		
		ОтборПоКурсу = ЭлементыОтбора.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборПоКурсу.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборПоКурсу.Использование = Истина;
		ОтборПоКурсу.ЛевоеЗначение = ПолеКД;		
		ОтборПоКурсу.ПравоеЗначение = ПараметрыСеанса.ТС_ТекущийПользователь;
		
	КонецЕсли;		
	
КонецПроцедуры

//*************************************************************
&НаКлиенте
Процедура НачатьТестирование(Команда)
	
	СтруктураПараметров = Новый Структура("Курс,Избранное",
		ЭтаФорма.Курс,Истина);
	ФормаТестирования = ПолучитьФорму("Обработка.ТС_Тестирование.Форма.Форма",СтруктураПараметров);
	ФормаТестирования.Открыть();
	
КонецПроцедуры



