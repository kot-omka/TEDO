///////////////////////////////////////////////////////////////
//

//*************************************************************
Процедура ОбработкаПроведения(Отказ, Режим)
	
	//Списки для РС ТС_ИзбранныеТесты
	СписокПравильных = Новый СписокЗначений;
	СписокОшибочных = Новый СписокЗначений;
	
	//Регистр сведений ТС_РезультатыТестирований
	Движения.ТС_РезультатыТестирований.Записывать = Истина;
	Для Каждого ТекСтрокаТесты Из Тесты Цикл
		
		Движение = Движения.ТС_РезультатыТестирований.Добавить();
		Движение.Пользователь = Пользователь;
		Движение.Курс = Курс;
		Движение.НаборТестов = НаборТестов;
		Движение.Тест = ТекСтрокаТесты.Тест;
		Движение.ЭтоПодтверждениеОтвета = ЭтоПодтверждениеОтвета;
		Движение.Ответ = ТекСтрокаТесты.Ответ;
		Движение.Правильный = ТекСтрокаТесты.Правильный;
		Движение.НомерТестирования = НомерТестирования;
		Движение.ПрограммаОбучения = ПрограммаОбучения;
		Движение.НомерЭтапа = НомерЭтапа;
		
		Если ТекСтрокаТесты.Правильный Тогда
			СписокПравильных.Добавить(ТекСтрокаТесты.Тест);
		Иначе
			СписокОшибочных.Добавить(ТекСтрокаТесты.Тест);
		КонецЕсли;
				
	КонецЦикла;
	
	//Регистр сведений ТС_ИзбранныеТесты
	Если Пользователь.ДобавлятьОшибочныеВИзбранные Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Тесты.Ссылка КАК Тест
		|ПОМЕСТИТЬ ВТ_ТЕСТЫ
		|ИЗ
		|	Справочник.ТС_Тесты КАК Тесты
		|ГДЕ
		|	Тесты.Ссылка В(&СписокОшибочных)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТС_ОшибочныеТесты.Тест
		|ИЗ
		|	РегистрСведений.ТС_ИзбранныеТесты КАК ТС_ОшибочныеТесты
		|ГДЕ
		|	ТС_ОшибочныеТесты.Пользователь = &Пользователь
		|	И ТС_ОшибочныеТесты.Курс = &Курс
		|	И НЕ ТС_ОшибочныеТесты.Тест В (&СписокПравильных)
		|	И ТС_ОшибочныеТесты.Авто
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Истина КАК Авто,
		|	&Пользователь КАК Пользователь,
		|	&Курс КАК Курс,
		|	Тесты.Тест
		|ИЗ
		|	ВТ_ТЕСТЫ КАК Тесты";
		
		Запрос.УстановитьПараметр("Курс", Курс);
		Запрос.УстановитьПараметр("Пользователь", Пользователь);
		Запрос.УстановитьПараметр("СписокПравильных", СписокПравильных);
		Запрос.УстановитьПараметр("СписокОшибочных", СписокОшибочных);
		
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();	
			
		НаборЗаписей = РегистрыСведений.ТС_ИзбранныеТесты.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Курс.Значение = Курс;
		НаборЗаписей.Отбор.Курс.Использование = Истина;
		НаборЗаписей.Отбор.Пользователь.Значение = Пользователь;
		НаборЗаписей.Отбор.Пользователь.Использование = Истина;
		НаборЗаписей.Отбор.Авто.Значение = Истина;
		НаборЗаписей.Отбор.Авто.Использование = Истина;
		НаборЗаписей.Загрузить(РезультатЗапроса);
		НаборЗаписей.Записать(Истина);
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	КоличествоОшибок = Тесты.НайтиСтроки(Новый Структура("Правильный", Ложь)).Количество();
КонецПроцедуры


