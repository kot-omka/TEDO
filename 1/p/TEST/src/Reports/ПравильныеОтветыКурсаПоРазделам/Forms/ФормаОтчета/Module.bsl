
&НаСервере
Функция НайтиРаздел(Курс, НужныйРаздел)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТС_Тесты.Ссылка
	               |ИЗ
	               |	Справочник.ТС_Тесты КАК ТС_Тесты
	               |ГДЕ
	               |	ТС_Тесты.Владелец = &Владелец
	               |	И ТС_Тесты.Наименование ПОДОБНО &ШаблонНаименование
	               |	И ТС_Тесты.ЭтоГруппа = ИСТИНА";
	Запрос.УстановитьПараметр("Владелец", Курс);
	Запрос.УстановитьПараметр("ШаблонНаименование", Формат(НужныйРаздел, "ЧЦ=2; ЧВН=") + "%");
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Возврат РезультатЗапроса.Выгрузить()[0].Ссылка;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура СледующийРаздел(Команда)
	ВыбратьРаздел(1);
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущийРаздел(Команда)
	ВыбратьРаздел(-1);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьРаздел(Сдвиг)
	ОтборКурс = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы[0];
	ОтборРаздел = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы[1];
	Если ОтборРаздел.ПравоеЗначение = Неопределено Тогда
		НужныйРазделЧисло = 1;
	Иначе
		НужныйРазделЧисло = Число(Лев(ОтборРаздел.ПравоеЗначение, 2)) + Сдвиг;
	КонецЕсли; 
	
	НужныйРаздел = НайтиРаздел(ОтборКурс.ПравоеЗначение, НужныйРазделЧисло);
	Если ЗначениеЗаполнено(НужныйРаздел) Тогда
		ОтборРаздел.ПравоеЗначение = НужныйРаздел;
		ОтборРаздел.Использование = Истина;
	КонецЕсли; 	
КонецПроцедуры
 
