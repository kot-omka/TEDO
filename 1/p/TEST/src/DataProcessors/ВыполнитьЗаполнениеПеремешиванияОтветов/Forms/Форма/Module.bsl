
&НаКлиенте
Процедура ВыполнитьОбработку(Команда)
	ВыполнитьОбработкуНаСервере();
	Сообщить("Обработка завершена");
КонецПроцедуры

&НаСервере
Процедура ВыполнитьОбработкуНаСервере()
	
	ТаблицаТестов.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТС_ТестыОтветы.Ссылка КАК ТестСсылка,
		|	ТС_ТестыОтветы.Ответ КАК Ответ
		|ИЗ
		|	Справочник.ТС_Тесты.Ответы КАК ТС_ТестыОтветы
		|ГДЕ
		|	НЕ ТС_ТестыОтветы.Ссылка.ПометкаУдаления
		|	И НЕ ТС_ТестыОтветы.Ссылка.Устаревший
		|	И ВЫБОР
		|			КОГДА &УказанКурс
		|				ТОГДА ТС_ТестыОтветы.Ссылка.Владелец = &Владелец
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТС_ТестыОтветы.Ссылка.Код
		|ИТОГИ ПО
		|	ТестСсылка";
	
	Запрос.УстановитьПараметр("Владелец", Объект.Курс);
	Запрос.УстановитьПараметр("УказанКурс", ЗначениеЗаполнено(Объект.Курс));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока Выборка.Следующий() Цикл
		
		ПеремешиватьОтветы = Истина;
		
		ВыборкаОтвет = Выборка.Выбрать();
		
		Пока ВыборкаОтвет.Следующий() Цикл
			
			ПеремешиватьОтветы = ЭтотВопросПеремешиваем(ВыборкаОтвет.Ответ);
			
			Если Не ПеремешиватьОтветы Тогда 
				Прервать;
			КонецЕсли;
			
		КонецЦикла;

		ТестСтрока = ТаблицаТестов.Добавить();
		ЗаполнитьЗначенияСвойств(ТестСтрока,Выборка);
		ТестСтрока.ПеремешиватьОтветы = ПеремешиватьОтветы;
		
	КонецЦикла;
	
	Для Каждого ТестСтрока Из ТаблицаТестов Цикл
		
		ТестОбъект = ТестСтрока.ТестСсылка.ПолучитьОбъект();
		ТестОбъект.ПеремешиватьОтветы = ТестСтрока.ПеремешиватьОтветы;
		
		Попытка
			ТестОбъект.Записать();
		Исключение
			Сообщить("Не удалось изменить статус """ + 
			Формат( ТестСтрока.ПеремешиватьОтветы,"БЛ='Не перемешивать'; БИ=Перемешивать") + 
			""" для теста " + ТестОбъект.ТестСсылка);
		КонецПопытки;
			
	КонецЦикла;
	
	
КонецПроцедуры

Функция ЭтотВопросПеремешиваем(Знач ОтветНаВопрос)
	
	ОтветВерхРег = ВРег(ОтветНаВопрос);
	
	Для Каждого ВариантОтвета Из СписокВариантов Цикл
		
		Если НЕ СтрНайти(ОтветВерхРег, ВРег(ВариантОтвета)) = 0 Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

