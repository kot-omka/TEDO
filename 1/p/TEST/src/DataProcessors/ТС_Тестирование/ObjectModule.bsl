//*************************************************************
Функция СформироватьРезультатТестирования() Экспорт
	
	//Првоерим количество тестов
	Если Тесты.Количество() = 0 Тогда
		Возврат "Нет тестов для создания документа!";
	КонецЕсли;
	
	//Переменная запрос
	Запрос = Новый Запрос;
	
	//Удалим тесты из избранных, если это необходимо
	Если Избранное И (УдалятьТестыИзИзбранного ИЛИ ИзбранноеАвто) Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТС_ИзбранныеТесты.Пользователь,
		|	ТС_ИзбранныеТесты.Курс,
		|	ТС_ИзбранныеТесты.Тест
		|ИЗ
		|	РегистрСведений.ТС_ИзбранныеТесты КАК ТС_ИзбранныеТесты
		|ГДЕ
		|	НЕ ТС_ИзбранныеТесты.Тест В (&ТестыИзТестирования)
		|	И ТС_ИзбранныеТесты.Пользователь = &Пользователь
		|	И ТС_ИзбранныеТесты.Курс = &Курс
		|	И ИСТИНА";
		
		Если ИзбранноеАвто Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"ИСТИНА","НЕ ТС_ИзбранныеТесты.Авто");
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Пользователь",Пользователь);
		Запрос.УстановитьПараметр("Курс",Курс);
		Запрос.УстановитьПараметр("ТестыИзТестирования",Тесты.ВыгрузитьКолонку("Тест"));
		
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();	
			
		НаборЗаписей = РегистрыСведений.ТС_ИзбранныеТесты.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Курс.Значение = Курс;
		НаборЗаписей.Отбор.Курс.Использование = Истина;
		НаборЗаписей.Отбор.Пользователь.Значение = Пользователь;
		НаборЗаписей.Отбор.Пользователь.Использование = Истина;
		НаборЗаписей.Загрузить(РезультатЗапроса);
		
		Попытка
			НаборЗаписей.Записать(Истина);
		Исключение
			Возврат ОписаниеОшибки();
		КонецПопытки;
	
	КонецЕсли;
		
	//Получим номер тестирования
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ISNULL(МАКСИМУМ(ТС_РезультатыТестирований.НомерТестирования),0) КАК НомерТестирования
	|ИЗ
	|	РегистрСведений.ТС_РезультатыТестирований КАК ТС_РезультатыТестирований
	|ГДЕ
	|	ТС_РезультатыТестирований.Пользователь = &Пользователь
	|	И ТС_РезультатыТестирований.Курс = &Курс
	|	И ТС_РезультатыТестирований.НаборТестов = &НаборТестов";	
	Запрос.УстановитьПараметр("Курс", Курс);
	Запрос.УстановитьПараметр("НаборТестов", НаборТестов);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);	
	ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать();
	Если  ВыборкаДетальныеЗаписи.Следующий() Тогда
		НомерТестирования = ВыборкаДетальныеЗаписи.НомерТестирования + 1;
	Иначе
		НомерТестирования = 1;
	КонецЕсли;
	
	//Создадим новый документ тестирования
	РезультатТестирования = Документы.ТС_РезультатТестирования.СоздатьДокумент();
	РезультатТестирования.Дата = ТекущаяДата();
	РезультатТестирования.Курс = Курс;
	РезультатТестирования.НаборТестов = НаборТестов;
	РезультатТестирования.Пользователь = Пользователь;
	РезультатТестирования.НомерТестирования = НомерТестирования;
	РезультатТестирования.ПрограммаОбучения = ПрограммаОбучения;
	РезультатТестирования.НомерЭтапа = НомерЭтапа;
	
	СтрокаОтвет = "";
	флОтветВерен = Истина;
	ПредыдущийТест = Тесты.Получить(0).Тест;
	СписокВерныхТестов = Новый СписокЗначений;	
	
	Для Каждого Строка ИЗ Тесты Цикл
		
		ТекТест = Строка.Тест;
		Если ТекТест <> ПредыдущийТест Тогда
			
			//Добавим строку в документ
			НоваяСтрокаДокумента = РезультатТестирования.Тесты.Добавить();
			НоваяСтрокаДокумента.Тест = ПредыдущийТест;
			НоваяСтрокаДокумента.Ответ = СтрокаОтвет;
			НоваяСтрокаДокумента.Правильный = флОтветВерен;
			
			//Добавим правильный ответ с список
			Если Избранное И (УдалятьТестыИзИзбранного ИЛИ ИзбранноеАвто) И флОтветВерен Тогда
				СписокВерныхТестов.Добавить(ПредыдущийТест);
			КонецЕсли;
			НомерТеста = Тесты.Индекс(Строка);
			
			СтрокаОтвет = "";
			флОтветВерен = Истина;
			ПредыдущийТест = ТекТест;
			
		КонецЕсли;
		
		Если Строка.Правильный 
			И флОтветВерен 
			И НЕ Строка.Выбор Тогда
				флОтветВерен = Ложь;
		КонецЕсли;
		
		Если Строка.Выбор Тогда
			СтрокаОтвет = ?(СтрДлина(СтрокаОтвет)=0,"",СтрокаОтвет + ", ") + Строка(Строка.НомерОтвета);
		КонецЕсли;	
		
	КонецЦикла;
	
	//Добавим последнюю строку в документ
	НоваяСтрокаДокумента = РезультатТестирования.Тесты.Добавить();
	НоваяСтрокаДокумента.Тест = ПредыдущийТест;
	НоваяСтрокаДокумента.Ответ = СтрокаОтвет;
	НоваяСтрокаДокумента.Правильный = флОтветВерен;
	
	//Добавим правильный ответ с список и отметим тесты в общем списке
	Если Избранное И (УдалятьТестыИзИзбранного ИЛИ ИзбранноеАвто) И флОтветВерен Тогда
		
		СписокВерныхТестов.Добавить(ПредыдущийТест);
		
		Для Каждого Строка ИЗ Тесты Цикл			
			Если СписокВерныхТестов.НайтиПоЗначению(Строка.Тест) <> Неопределено Тогда
				Строка.ЕстьВИзбранном = Ложь;
			КонецЕсли;			
		КонецЦикла	
		
	КонецЕсли;
	
	Попытка
		РезультатТестирования.Записать(РежимЗаписиДокумента.Проведение);
		Возврат Истина;
	Исключение
		Возврат ОписаниеОшибки();		
	КонецПопытки;	
	
КонецФункции
