//*************************************************************
Функция ПечатьПодтверждения(Тест) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	//Проверим значение параметра
	Если ТипЗнч(Тест) <> Тип("СправочникСсылка.ТС_Тесты") Тогда
		Возврат ТабличныйДокумент;
	КонецЕсли;	
	
	МакетПодтверждения = ПолучитьМакет("МакетПодтверждения");
	
	//Шапка
	Область = МакетПодтверждения.ПолучитьОбласть("Шапка");
	Область.Параметры.Курс = Тест.Владелец;
	Область.Параметры.Тест = Тест;	
	ТабличныйДокумент.Вывести(Область);
	
	//Шапка подтверждения
	ОбластьСсылка = МакетПодтверждения.ПолучитьОбласть("ПодтверждениеСсылка");
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТС_РезультатыТестирований.Регистратор КАК Ссылка,
	|	ТС_РезультатыТестирований.Регистратор.ТС_Комментарий КАК Описание
	|ИЗ
	|	РегистрСведений.ТС_РезультатыТестирований КАК ТС_РезультатыТестирований
	|ГДЕ
	|	ТС_РезультатыТестирований.ЭтоПодтверждениеОтвета
	|	И ТС_РезультатыТестирований.Тест = &Тест
	|	И ТС_РезультатыТестирований.Регистратор.Дата >= &ДатаАктуальности
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТС_РезультатыТестирований.Регистратор.Дата УБЫВ";
	
	Запрос.УстановитьПараметр("Тест", Тест);
	Запрос.УстановитьПараметр("ДатаАктуальности", Тест.Владелец.ДатаАктуальности);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		Область = МакетПодтверждения.ПолучитьОбласть("НетПодтверждений");
		ТабличныйДокумент.Вывести(Область);
		
	Иначе
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			ОбластьСсылка.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
			ТабличныйДокумент.Вывести(ОбластьСсылка);
			
		КонецЦикла;
		
	КонецЕсли;	

	Возврат ТабличныйДокумент;	
	
КонецФункции
