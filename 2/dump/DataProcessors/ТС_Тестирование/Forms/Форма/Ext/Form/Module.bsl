///////////////////////////////////////////////////////////////
// СОБЫТИЯ ФОРМЫ

//*************************************************************
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.Пользователь = ПараметрыСеанса.ТС_ТекущийПользователь;
	
КонецПроцедуры

//*************************************************************
&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	//прочитаем параметры
	Если Параметры.Свойство("Курс") Тогда
		Объект.Курс = Параметры.Курс;
	КонецЕсли;
	Если Параметры.Свойство("Избранное") Тогда
		Объект.Избранное = Параметры.Избранное;
	КонецЕсли;
	
	//Количество тестов по умолчанию
	Если Объект.КоличествоТестов = 0 Тогда 
		Объект.КоличествоТестов = 14;
		Объект.УдалятьТестыИзИзбранного = Истина;
	КонецЕсли;
	
	//Для избранного нет программы обучения и набора тестов
	Если Объект.Избранное Тогда
		Объект.ПрограммаОбучения = Справочники.ТС_ПрограммыОбучения.ПустаяСсылка();
		Объект.НаборТестов = Справочники.ТС_НаборыТестов.ПустаяСсылка();
	КонецЕсли;
	
	Элементы.ГруппаИзбранное.Видимость = Объект.Избранное;
	Элементы.ГруппаПрограммаНабор.Видимость = Не Объект.Избранное;
		
КонецПроцедуры


///////////////////////////////////////////////////////////////
// ДЕЙСТВИЯ ФОРМЫ

//*************************************************************
&НаКлиенте
Процедура НачатьТестирование(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Пользователь) Тогда
		ПоказатьПредупреждение(,"Выберите пользователя!");
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Курс) Тогда
		ПоказатьПредупреждение(,"Выберите курс!");
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ПрограммаОбучения) 
		И Не ЗначениеЗаполнено(Объект.НаборТестов)
		И Не Объект.Избранное Тогда
		ПоказатьПредупреждение(,"Выберите программу обучени или набор тестов!");
		Возврат;
	КонецЕсли;
	
	//Отобразим состояние
	Состояние("Идет построение формы со списком тестов. Подождите немного...");
	
	//Откроем форму тестирования
	ЭтаФорма.Закрыть();
	СтруктураПараметров = Новый Структура(
		"Курс,Пользователь,ПрограммаОбучения,НаборТестов,
		|Избранное,КоличествоТестов,УдалятьТестыИзИзбранного",
		Объект.Курс,Объект.Пользователь,Объект.ПрограммаОбучения,Объект.НаборТестов,
		Объект.Избранное,Объект.КоличествоТестов,Объект.УдалятьТестыИзИзбранного);
	ФормаТестирования = ПолучитьФорму("Обработка.ТС_Тестирование.Форма.ФормаТестирования",СтруктураПараметров);
	ФормаТестирования.Открыть();
	
КонецПроцедуры

//*************************************************************
&НаКлиенте
Процедура ПрограммаОбученияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ПрограммаОбучения) Тогда
		Объект.НаборТестов = "";
	КонецЕсли;
		
КонецПроцедуры

//*************************************************************
&НаКлиенте
Процедура НаборТестовПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.НаборТестов) Тогда
		Объект.ПрограммаОбучения = "";
	КонецЕсли;	
	
КонецПроцедуры

