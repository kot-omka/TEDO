///////////////////////////////////////////////////////////////
// ПРЕДОПРЕДЕЛЕННЫЕ ПРОЦЕДУРЫ

//*************************************************************
Процедура УстановкаПараметровСеанса()
	
	//Параметр ТекущийПользователь
	ПользовательИБ = ПользователиИнформационнойБазы.ТекущийПользователь();
	
	//Создадим пользователя, если необходимо
	Если НЕ ЗначениеЗаполнено(ПользовательИБ.Имя) Тогда
		ПользовательИБ = ПользователиИнформационнойБазы.СоздатьПользователя();
		ПользовательИБ.Имя = "Администратор";
		ПользовательИБ.ПолноеИмя = "Администратор";
		ПользовательИБ.ПоказыватьВСпискеВыбора = Истина;
		ПользовательИБ.АутентификацияСтандартная = Истина;
		ПользовательИБ.Роли.Добавить(Метаданные.Роли.ТС_ПолныеПрава);
		Попытка
			ПользовательИБ.Записать();
			//т. к. это первый запуск, установми константы
			Константы.ТС_ИспользоватьЗамечания.Установить(Истина);
			Константы.ТС_ИспользоватьИзбранныеТесты.Установить(Истина);
			Константы.ТС_ИспользоватьНаборыТестов.Установить(Истина);
			Константы.ТС_ИспользоватьПрограммыОбучения.Установить(Истина);
			Сообщить("Выполнены начальные настройки программы!");
		Исключение
			Сообщить(ОписаниеОшибки());
			Возврат;
		КонецПопытки;
	КонецЕсли;		
		
	//Найдем пользователя в справочнике		
	Если СтрДлина(ПользовательИБ.Имя) > 50 Тогда
		Возврат;
	КонецЕсли;	
	ТекПользователь = Справочники.ТС_Пользователи.НайтиПоКоду(ПользовательИБ.Имя);
	Если ТекПользователь = Справочники.ТС_Пользователи.ПустаяСсылка() Тогда
		//Создадим пользователя, если не нашли
		ТекПользователь = Справочники.ТС_Пользователи.СоздатьЭлемент();
		ТекПользователь.Код = ПользовательИБ.Имя;
		ТекПользователь.Наименование = ПользовательИБ.ПолноеИмя;
		ТекПользователь.ПеремешиватьОтветыВТесте = Истина;
		ТекПользователь.ДобавлятьОшибочныеВИзбранные = Истина;
		Попытка
			ТекПользователь.Записать();
			Сообщить("Создан новый пользователь: " + ТекПользователь.Код);
		Исключение
			Сообщить(ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
	ПараметрыСеанса.ТС_ТекущийПользователь = ТекПользователь.Ссылка;
	
КонецПроцедуры