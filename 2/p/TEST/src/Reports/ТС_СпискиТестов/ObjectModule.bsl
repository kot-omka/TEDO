#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

//////////////////////////////////////////////////////////////
// КОМПОНОВКА РЕЗУЛЬТАТА

//***************************************************************************************
Процедура ДобавитьЗначениеПараметра(Параметр, Значение, ВключатьПустые = Истина, НастройкиОтчета = Неопределено)
	
	Если Значение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НастройкиОтчета = Неопределено Тогда
		НастройкиОтчета = ЭтотОбъект.КомпоновщикНастроек.ПолучитьНастройки();
	КонецЕсли;
	
	Объект = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(Параметр));
	Если Объект <> Неопределено Тогда
		Объект.Значение = Значение;
		Объект.Использование = ЗначениеЗаполнено(Значение) ИЛИ (ВключатьПустые);	
	КонецЕсли;
	
КонецПроцедуры

//***************************************************************************************
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	///////////////////////////////////////////
	//Проверим и добавим значения параметров
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	ДобавитьЗначениеПараметра("Курс",ЭтотОбъект.Курс,,НастройкиОтчета);
	ДобавитьЗначениеПараметра("ДатаАктуальности",ЭтотОбъект.Курс.ДатаАктуальности,,НастройкиОтчета);
	ДобавитьЗначениеПараметра("НаборТестов",ЭтотОбъект.НаборТестов,Истина,НастройкиОтчета);
	ДобавитьЗначениеПараметра("ГруппаТестов",ЭтотОбъект.ГруппаТестов,Истина,НастройкиОтчета);
	
	///////////////////////////////////////////
	//Вывод отчета
	ДокументРезультат.Очистить();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(ЭтотОбъект.СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	//Создадим и инициализируем процессор компоновки
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	//Обозначим начало вывода
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
