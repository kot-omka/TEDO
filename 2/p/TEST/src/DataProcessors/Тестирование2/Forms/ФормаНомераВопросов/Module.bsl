
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Режим = Параметры.Режим;
	СимуляцияЭкзамена = Параметры.СимуляцияЭкзамена;
	Результат.Загрузить(ПолучитьИзВременногоХранилища(Параметры.ХранилищеРезультат));  
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ПрограммноЗаполнитьФорму();
	Иначе
		Отказ = Истина;
		Сообщить("Отсутствуют вопросы");
	КонецЕсли;
	
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОпределитьВидимостьГруппыНомеров();
КонецПроцедуры

&НаКлиенте
Процедура НомерВопросаИзБазыПриИзменении(Элемент)
	ОпределитьВидимостьГруппыНомеров();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВопрос(Команда)
	НомерВопроса = Число(Прав(ЭтотОбъект.ТекущийЭлемент.Заголовок,3));
	ЗавершитьВыборВопроса(НомерВопроса);
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	ЗавершитьВыборВопроса();
КонецПроцедуры

&НаСервере
Процедура ПрограммноЗаполнитьФорму()
	
	СоздатьКоманду();
	
	НомерГруппы = 1;
	КоличествоКнопокВРяду = 1;
	Расположение = СоздатьГруппу("Группа" + НомерГруппы, Элементы.ГруппаВопросы);
	РасположениеПорядковыйНомер = СоздатьГруппу("ГруппаПорядковыйНомер" + НомерГруппы, Элементы.ГруппаВопросыПорядковыйНомер);
		
	НомерВопроса = 1;
	Для Каждого СтрокаРезультат Из Результат Цикл
		
		//НомерВопроса = Число(Прав(СтрокаРезультат.Номер, 3));
		
		
		СоздатьКнопку( "Команда" + СтрокаРезультат.Номер, СтрокаРезультат.Номер, Расположение, СтрокаРезультат);
		СоздатьКнопку( "КомандаПорядковыйНомер" + НомерВопроса, НомерВопроса, РасположениеПорядковыйНомер, СтрокаРезультат);
		
		
		Если КоличествоКнопокВРяду % 10 = 0 Тогда
			НомерГруппы = НомерГруппы + 1;
			Расположение = СоздатьГруппу("Группа" + НомерГруппы, Элементы.ГруппаВопросы);
			РасположениеПорядковыйНомер = СоздатьГруппу("ГруппаПорядковыйНомер" + НомерГруппы, Элементы.ГруппаВопросыПорядковыйНомер);
		КонецЕсли;
		
		КоличествоКнопокВРяду = КоличествоКнопокВРяду + 1;
		НомерВопроса = НомерВопроса + 1
	КонецЦикла;
	
	
КонецПроцедуры

&НаСервере
Процедура СоздатьКоманду()
	
	НоваяКоманда = Команды.Добавить("ВыбратьВопросНаФорме");
	НоваяКоманда.Заголовок = "Выбрать вопрос";
	НоваяКоманда.Действие = "ВыбратьВопрос";
	
КонецПроцедуры

&НаСервере
Функция СоздатьГруппу(НаименованиеГруппы, Владелец)
	
	Группа = ЭтаФорма.Элементы.Добавить(НаименованиеГруппы, Тип("ГруппаФормы"), Владелец);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.Отображение = ОтображениеОбычнойГруппы.Нет;
	Группа.ОтображатьЗаголовок = Ложь; 
	Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;

	Возврат Группа;
	
КонецФункции

&НаСервере
Процедура СоздатьКнопку(ИмяКоманды, НомерВопроса, Расположение, СтрокаРезультат)
	
	
	ИмяКоманды = ИмяКоманды;
	
	НовыйЭлемент = Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"),Расположение);
	НовыйЭлемент.Заголовок = НомерВопроса;
	НовыйЭлемент.ИмяКоманды = "ВыбратьВопросНаФорме";
	НовыйЭлемент.Ширина = 7;
	
	
	Если Режим = "Экзамен" И СимуляцияЭкзамена И СтрокаРезультат.ДанОтвет Тогда
		НовыйЭлемент.ЦветФона = ЦветОтветДан();
	ИначеЕсли СтрокаРезультат.ОтветПоказан Тогда
		НовыйЭлемент.ЦветФона = ЦветОтветДан();
	ИначеЕсли СтрокаРезультат.ДанОтвет И СтрокаРезультат.ОтветВерный Тогда
		НовыйЭлемент.ЦветФона = ЦветОтветДанВерно();
	ИначеЕсли СтрокаРезультат.ДанОтвет И НЕ СтрокаРезультат.ОтветВерный Тогда
		НовыйЭлемент.ЦветФона = ЦветОтветДанНеВерно();
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Функция ЦветОтветДан()
	Возврат Новый Цвет(217, 237, 247);
КонецФункции

&НаСервере
Функция ЦветОтветДанВерно()
	Возврат Новый Цвет(223, 240, 216);
КонецФункции

&НаСервере
Функция ЦветОтветДанНеВерно()
	Возврат Новый Цвет(242, 222, 222);
КонецФункции

&НаКлиенте
Процедура ЗавершитьВыборВопроса(ПараметрыЗакрытия = Неопределено)
	Закрыть(ПараметрыЗакрытия);
КонецПроцедуры

&НаСервере
Процедура ОпределитьВидимостьГруппыНомеров()
	Элементы.ГруппаВопросы.Видимость = ОтобразитьНомерВопросаИзСправочникаТесты;
	Элементы.ГруппаВопросыПорядковыйНомер.Видимость = Не ОтобразитьНомерВопросаИзСправочникаТесты;
КонецПроцедуры




